plugins {
    id 'com.moowork.node' version '1.3.1'
}

version = '1.0.0'

dependencies {
    compile project(':hashtagmap-core')
    implementation project(':hashtagmap-common')
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'io.rest-assured:rest-assured:3.3.0'
}

jacocoTestCoverageVerification {
    def Qdomains = []
    for (qPattern in "*.QA".."*.QZ") {
        Qdomains.add(qPattern + "*")
    }

    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }

            excludes = [] + Qdomains
        }

        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }

            excludes = [] + Qdomains
        }
    }
}

node {
    workDir = file("./front")
    npmWorkDir = file("./front")
    nodeModulesDir = file("./front")
}

task copyKakaoApiKey(type: Copy) {
    description = "Copy kakao map api key from hashtagmap-secret"
    from '../hashtagmap-secret/kakao/index.js'
    into 'front/src/secret/'
}

task setUp(type: NpmTask, dependsOn: copyKakaoApiKey) {
    description = "Install Node.js Package"
    args = ['install']
}

task buildFrontEnd(type: NpmTask, dependsOn: setUp) {
    description = "Build vue.js"
    args = ['run', 'build']
}

processResources.dependsOn 'copyKakaoApiKey'
processResources.dependsOn 'buildFrontEnd'
