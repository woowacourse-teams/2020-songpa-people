plugins {
    id 'com.moowork.node' version '1.3.1'
}

version = '2.0.0'

dependencies {
    implementation project(':hashtagmap-core')
    implementation project(':hashtagmap-common')
    implementation project(':hashtagmap-kakao-scheduler')
    implementation project(':hashtagmap-instagram-service')
    implementation project(':hashtagmap-slack')

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'io.rest-assured:spring-mock-mvc:3.3.0'
}

jacocoTestCoverageVerification {
    def Qdomains = []
    for (qPattern in "*.QA".."*.QZ") {
        Qdomains.add(qPattern + "*")
    }

    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }

            excludes = [] + Qdomains
        }

        rule {
            enabled = true
            element = "CLASS"

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }

            excludes = [] + Qdomains
        }
    }
}

node {
    workDir = file("./front")
    npmWorkDir = file("./front")
    nodeModulesDir = file("./front")
}

task setUp(type: NpmTask, dependsOn: copySlackProperties) {
    description = "Install Node.js Package"
    args = ['install']
}

task copySlackProperties(type: Copy) {
    description = "Copy application-slack-admin.yml from hashtagmap-secret"
    from '../hashtagmap-secret/slack/application-admin.yml'
    into 'src/main/resources/'
}

task buildFrontEnd(type: NpmTask, dependsOn: setUp) {
    description = "Build vue.js"
    args = ['run', 'build']
}

processResources.dependsOn 'buildFrontEnd'

sonarqube {
    properties {
        property "sonar.exclusions", "**/*Test*.*, **/Q*.java, **/*Doc*.java"
    }
}
